buildscript {
    ext.kotlin_version = '1.1.3-2'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.1.3-2'
    id "com.gradle.plugin-publish" version "0.9.7"
    id "com.jfrog.bintray" version "1.7.3"
    id "com.jfrog.artifactory" version "4.4.18"
}

apply plugin: 'maven-publish'
apply plugin: 'org.junit.platform.gradle.plugin'

group = 'com.github.nicholashauschild'
version = '0.3.0-SNAPSHOT'

def bintrayUsername = hasProperty('bintray.username') ? project.getProperty('bintray.username') : ''
def bintrayApiKey = hasProperty('bintray.apikey') ? project.getProperty('bintray.apikey') : ''
def isSnapshot = version.endsWith('SNAPSHOT')
def isRelease = !isSnapshot

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    testCompile gradleTestKit()
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile 'org.jetbrains.spek:spek-api:1.1.2'

    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.2'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.1'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

def pomConfig = {
    name 'gradle-dependency-info-plugin'
    url 'https://github.com/nicholashauschild/gradle-dependency-info-plugin'
    licenses {
        license {
            name 'MIT License'
            url 'https://opensource.org/licenses/mit-license.php'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'nicholashauschild'
            name 'Nicholas Hauschild'
            email 'nicholas.hauschild@gmail.com'
        }
    }
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Gradle plugin to package dependency details with a jar')
                root.children().last() + pomConfig
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = bintrayUsername
            password = bintrayApiKey
        }
        defaults {
            publications 'plugin'
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey

    publications = ['plugin']

    pkg {
        repo = 'maven'
        name = 'gradle-dependency-info-plugin'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/nicholashauschild/gradle-dependency-info-plugin.git'

        desc = 'Gradle plugin to include dependency information in generated jar'
        labels = ['gradle', 'spring-boot', 'plugin', 'java']
        websiteUrl = 'https://github.com/nicholashauschild/gradle-dependency-info-plugin'
        issueTrackerUrl = 'https://github.com/nicholashauschild/gradle-dependency-info-plugin/issues'
    }
}

pluginBundle {
    website = 'https://github.com/nicholashauschild/gradle-dependency-info-plugin'
    vcsUrl = 'https://github.com/nicholashauschild/gradle-dependency-info-plugin'

    plugins {
        plugin {
            id = 'com.github.nicholashauschild.dependency-info'
            displayName = 'Dependency Info Plugin'

            description = 'Gradle plugin to include dependency information in generated jar'
            tags = ['java', 'spring-boot']
        }
    }
}