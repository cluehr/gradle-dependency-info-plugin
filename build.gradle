buildscript {
    ext.kotlin_version = '1.1.3-2'

    repositories {
        mavenCentral()
    }

    dependencies {

    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.1.3-2'
    id "com.gradle.plugin-publish" version "0.9.7"
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'maven-publish'

group = 'com.github.nicholashauschild'
version = '0.3.0-SNAPSHOT'

def bintrayUsername = hasProperty("bintray.username") ? project.getProperty("bintray.username") : ""
def bintrayApiKey = hasProperty("bintray.apikey") ? project.getProperty("bintray.apikey") : ""

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testCompile 'org.jetbrains.spek:spek-api:1.1.2'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.1'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

def pomConfig = {
    name 'gradle-dependency-info-plugin'
    url 'https://github.com/nicholashauschild/gradle-dependency-info-plugin'
    licenses {
        license {
            name 'MIT License'
            url 'https://opensource.org/licenses/mit-license.php'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'nicholashauschild'
            name 'Nicholas Hauschild'
            email 'nicholas.hauschild@gmail.com'
        }
    }
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Gradle plugin to package dependency details with a jar')
                root.children().last() + pomConfig
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey

    publications = ['plugin']

    pkg {

    }
}

pluginBundle {
    website = 'https://github.com/nicholashauschild/gradle-dependency-info-plugin'
    vcsUrl = 'https://github.com/nicholashauschild/gradle-dependency-info-plugin'

    plugins {
        plugin {
            id = 'com.github.nicholashauschild.dependency-info'
            displayName = 'Dependency Info Plugin'

            description = 'Gradle plugin to include dependency information in generated jar'
            tags = ['java', 'spring-boot']
        }
    }
}